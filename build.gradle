plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
}

repositories {
    jcenter()
    flatDir {
        dirs 'lib'
    }
}

defaultTasks "tinkerGraphApp"

dependencies {
    // Soot for parsing byte-code
    implementation 'org.soot-oss:soot:4.2.1'
    // Logging dependencies
    implementation 'org.apache.logging.log4j:log4j-core:2.8.2'
    implementation 'org.apache.logging.log4j:log4j-slf4j-impl:2.8.2'
    // Gremlin dependencies
    implementation 'org.apache.tinkerpop:gremlin-core:3.4.6'
    // Graph database dependencies
    implementation 'org.apache.tinkerpop:tinkergraph-gremlin:3.4.6'
    implementation 'org.janusgraph:janusgraph-driver:0.5.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.11.2'
    implementation 'khttp:khttp:1.0.0'
    // Kotlin dependencies for Plume
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.3.72"
    // Plume library
    implementation name: 'plume-driver-X.X.X'
    implementation name: 'plume-extractor-X.X.X'
}

ext {
    tinkerGraphMainClass = "com.example.TinkerGraphApp"
    janusGraphMainClass = "com.example.JanusGraphApp"
    tigerGraphMainClass = "com.example.TigerGraphApp"
    neptuneMainClass = "com.example.NeptuneApp"
}

task tinkerGraphApp(type: JavaExec) {
    group = "Execution"
    description = "Run an example app using the TinkerGraph backend."
    classpath = sourceSets.main.runtimeClasspath
    main = tinkerGraphMainClass
}

task janusGraphApp(type: JavaExec) {
    group = "Execution"
    description = "Run an example app using the JanusGraph backend."
    classpath = sourceSets.main.runtimeClasspath
    main = janusGraphMainClass
}

task tigerGraphApp(type: JavaExec) {
    group = "Execution"
    description = "Run an example app using the TigerGraph backend."
    classpath = sourceSets.main.runtimeClasspath
    main = tigerGraphMainClass
}

task neptuneApp(type: JavaExec) {
    group = "Execution"
    description = "Run an example app using the Amazon Neptune backend."
    classpath = sourceSets.main.runtimeClasspath
    main = neptuneMainClass
}